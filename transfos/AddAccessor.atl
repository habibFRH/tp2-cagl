-- @path ClassDiagram=/ClassDiagram/model/ClassDiagram.ecore

module AddAccessorRefining;
create cible : ClassDiagram from source : ClassDiagram;

-- ****************************************************************
-- Helpers utilis�s par la transformation 

-- retourne la chaine courante avec premier caract�re en majuscule
helper context String def: firstToUpper() : String =
	self.substring(1, 1).toUpper() + self.substring(2, self.size());

-- retourne la r�f�rence sur le type void (suppos� unique et pr�sent)
helper def: voidType : ClassDiagram!VoidType =
	ClassDiagram!VoidType.allInstances() -> asSequence() -> first();

-- v�rifie qu'un attribut poss�de un getter 
helper context ClassDiagram!Attribute def: hasGetter() : Boolean = 
	self.owner.methods -> exists ( m |
		m.name = 'get' + self.name.firstToUpper() and
		m.paramNames -> isEmpty() and
		m.paramTypes -> isEmpty() and
		m.returnType = self.type  and 
		m.debug('Test pour l\'attribut '+self.name+' de la méthode') = m
 	);

-- v�rifie qu'un attribut poss�de un setter 
helper context ClassDiagram!Attribute def: hasSetter() : Boolean = 
	self.owner.methods -> exists ( m |
		m.name = 'set' + self.name.firstToUpper() and
		m.paramNames -> size() = 1 and
		m.paramTypes -> includes( self.type ) and
		m.returnType = thisModule.voidType
 	);

-- ********************************************************************
-- Duplication de tous les �l�ments du mod�le � l'identique, sauf les attributs 
-- car il faut d'abord v�rifier s'ils ont un setter ou un getter pour les g�n�rer
-- en compl�ment.

rule duplicateModelBase {
from
	sourceBase : ClassDiagram!ModelBase
to
	cibleBase : ClassDiagram!ModelBase (
		allClasses <- sourceBase.allClasses,
		allInterfaces <- sourceBase.allInterfaces,
		allAssociations <- sourceBase.allAssociations,
		voidT <- sourceBase.voidT,
		intT <- sourceBase.intT,
		stringT <- sourceBase.stringT,
		boolT <- sourceBase.boolT
	)
}

rule duplicateClass {
from
	sourceClass : ClassDiagram!Class
to
	cibleClass : ClassDiagram!Class (
		name <- sourceClass.name,
		attributes <- sourceClass.attributes,
		methods <- sourceClass.methods,
		interfaces <- sourceClass.interfaces,
		associations <- sourceClass.associations
	)
do{
	('On duplique la classe '+sourceClass.name).println();
  }
}

rule duplicateInterface  {
from
	interfaceSource : ClassDiagram!Interface
to
	interfaceCible : ClassDiagram!Interface (
		name <- interfaceSource.name,
		methods <- interfaceSource.methods,
		implementedBy <- interfaceSource.implementedBy
	)
}

rule duplicateMethod {
from
	methodSource : ClassDiagram!Method
to
	methodCible : ClassDiagram!Method (
		name <- methodSource.name,
		owner <- methodSource.owner,
		returnType <- methodSource.returnType,
		paramTypes <- methodSource.paramTypes,
		paramNames <- methodSource.paramNames
	)
}

rule duplicateAssociationEnd {
from
	aeSource : ClassDiagram!AssociationEnd
to
	aeCible : ClassDiagram!AssociationEnd (
		name <- aeSource.name,
		lowerBound <- aeSource.lowerBound,
		upperBound <- aeSource.upperBound,
		_class <- aeSource._class,
		association <- aeSource.association
	)
}

rule duplicateAssociation {
from
	associationSource : ClassDiagram!Association 
to
	associationCible : ClassDiagram!Association (
		name <- associationSource.name,
		ends <- associationSource.ends
	)
}

rule duplicationVoidType {
from
	voidTypeSource : ClassDiagram!VoidType 
to
	voidTypeCible : ClassDiagram!VoidType (
		name <- voidTypeSource.name
	)
}

rule duplicationIntegerType {
from
	integerTypeSource : ClassDiagram!IntegerType 
to
	integerTypeCible : ClassDiagram!IntegerType (
		name <- integerTypeSource.name
	)
}

rule duplicationStringType {
from
	stringTypeSource : ClassDiagram!StringType 
to
	stringTypeCible : ClassDiagram!StringType (
		name <- stringTypeSource.name
	)
}

rule duplicationBooleanType {
from
	booleanTypeSource : ClassDiagram!BooleanType 
to
	booleanTypeCible : ClassDiagram!BooleanType (
		name <- booleanTypeSource.name
	)
}


-- **********************************************************************
-- Duplication des attributs, avec cr�ation au besoin des m�thodes getter
-- et setter associ�es. 4 r�gles selon que l'attribut poss�de :
-- 1) D�j� son getter et son setter
-- 2) Un setter mais pas de getter
-- 3) Un getter mais pas de setter
-- 4) Ni setter ni getter

rule attributeHasAll {
from 
	attSource : ClassDiagram!Attribute (
		attSource.hasSetter() and attSource.hasGetter())
to
	attTarget : ClassDiagram!Attribute (
		name <- attSource.name,
		owner <- attSource.owner,
		type <- attSource.type,
		multiplicity <- attSource.multiplicity
	)
}
	
rule attributeHasSetter {
from 
	attSource : ClassDiagram!Attribute (
		attSource.hasSetter() and not(attSource.hasGetter())
	)
to
	attTarget : ClassDiagram!Attribute (
		name <- attSource.name,
		owner <- attSource.owner,
		type <- attSource.type,
		multiplicity <- attSource.multiplicity
	),	
	getter : ClassDiagram!Method (
		name <- 'get' + attSource.name.firstToUpper(),
		returnType <- attTarget.type,
		owner <- attTarget.owner
	)
}

rule attributeHasGetter {
from
	attSource : ClassDiagram!Attribute (
		not(attSource.hasSetter()) and attSource.hasGetter()
	)
to
	attTarget : ClassDiagram!Attribute (
		name <- attSource.name,
		owner <- attSource.owner,
		type <- attSource.type,
		multiplicity <- attSource.multiplicity
	),		
	setter : ClassDiagram!Method (
		name <- 'set' + attSource.name.firstToUpper(),
		returnType <- thisModule.voidType,
		owner <- attTarget.owner,
		paramNames <- Set { 'value' },
		paramTypes <- Set { attTarget.type }
	) 
}

rule attributeHasNothing {
from
	attSource : ClassDiagram!Attribute (
		not(attSource.hasSetter()) and not(attSource.hasGetter())
	)
to
	attTarget : ClassDiagram!Attribute (
		name <- attSource.name,
		owner <- attSource.owner,
		type <- attSource.type,
		multiplicity <- attSource.multiplicity
	),		
	setter : ClassDiagram!Method (
		name <- 'set' + attSource.name.firstToUpper(),
		returnType <- thisModule.voidType,
		owner <- attTarget.owner,
		paramNames <- Set { 'value' },
		paramTypes <- Set { attTarget.type }
	),	
	getter : ClassDiagram!Method (
		name <- 'get' + attSource.name.firstToUpper(),
		returnType <- attTarget.type,
		owner <- attTarget.owner
	)
}
